// ========== GRADLE PLUGINS / REPOSITORIES SETUP
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // STUFF FOR BINTRAY
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'project-report'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: classes) {
        classifier = 'javadoc'
        from javadoc
    }

    javadoc {
        failOnError = false
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    repositories {
        jcenter()
    }

    idea {
        module {
            downloadSources = true
        }
    }

    test {
        // Minimize console spam while running tests without swallowing critical debugging info.
        testLogging {
            exceptionFormat "FULL"
            events "passed", "skipped", "failed"
            displayGranularity = 0
            showExceptions true
            showCauses true
            showStackTraces true
        }

        ignoreFailures = false
    }

    // http://www.gradle.org/docs/current/userguide/java_plugin.html
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

// Disable jar, sourcesJar, and javadoc tasks for the root project - we only want them to run for submodules
jar.enabled = false
sourcesJar.enabled = false
javadocJar.enabled = false

// ========== PROPERTIES FOR GRADLE BUILD - DEPENDENCY VERSIONS / ETC
ext {
    slf4jVersion = '1.7.7'
    javaxInjectVersion = '1'
    javaxValidationVersion = '1.0.0.GA'
    servletApiVersion = '3.0.1'
    springVersion = '4.3.2.RELEASE'
    jersey1Version = '1.19.2'
    jersey2Version = '2.23.2'
    jaxRsVersion = '2.0.1'

    junitVersion = '4.12'
    mockitoVersion = '1.9.5'
    logbackVersion = '1.1.2'
    jacksonVersion = '2.4.2'
    assertJVersion = '3.12.2'
    junitDataproviderVersion = '1.10.1'
    hamcrestVersion = '1.3'
    hibernateValidatorVersion = '4.3.0.Final'

    jetbrainsAnnotationsVersion = '16.0.3'

    orgReflectionsVersion = '0.9.11'
    javassistVersion = '3.18.2-GA'

    jettyVersion = '9.2.19.v20160908'
    restAssuredVersion = '3.0.1'

    // JACOCO PROPERTIES
    jacocoToolVersion = '0.8.0'
    // Anything in this jacocoExclusions list will be excluded from coverage reports. The format is paths to class
    //      files, with wildcards allowed. e.g.: jacocoExclusions = [ "com/nike/Foo.class", "**/Bar*.*" ]
    jacocoExclusions = []
    jacocoCoverageThresholdSetup = {
        configure(subprojects.findAll { isSubprojectIncludedInJacocoReports(it) }) {
            // Configure the minimum code coverage rules.
            jacocoTestCoverageVerification { JacocoCoverageVerification v ->
                violationRules {
                    rule { JacocoViolationRule r ->
                        enabled = true
                        limit {
                            minimum = 0.9
                            counter = "INSTRUCTION"
                        }

                    }

                    rule { JacocoViolationRule r ->
                        enabled = true
                        limit {
                            minimum = 0.9
                            counter = "BRANCH"
                        }
                    }
                }
            }
        }
    }
    // Configure which subprojects we're doing jacoco for.
    isSubprojectIncludedInJacocoReports = { Project subProj ->
        // For this repo we'll include everything that's not a sample or testonly module.
        return !subProj.name.startsWith("sample") && !subProj.getName().startsWith("testonly")
    }

    // BINTRAY STUFF
    bintrayUser = project.hasProperty('bintrayUser') ? property('bintrayUser') : 'UNDEFINED'
    bintrayKey = project.hasProperty('bintrayKey') ? property('bintrayKey') : 'UNDEFINED'
    bintrayVersion = "$version"
}

// ========== COMBO TEST REPORT - View the combined/merged report at: [project_root]/build/reports/tests/index.html
apply from: file(rootProject.projectDir.getAbsolutePath() + '/gradle/junitComboTestReport.gradle')

// ========== JACOCO SETUP - View the combined/merged report at: [project_root]/build/reports/jacoco/jacocoRootReport/html/index.html.
//                           Individual reports for each submodule can be found at: [project_root]/[submodule]/build/reports/jacoco/test/html/index.html
apply from: file(rootProject.projectDir.getAbsolutePath() + '/gradle/jacoco.gradle')

// ========== BINTRAY PUBLISHING
apply from: file(rootProject.projectDir.getAbsolutePath() + '/gradle/bintrayPublishing.gradle')

// ========== MISCELLANEOUS BUILD STUFF
allprojects {
    group = groupId // Necessary for the maven install task to function correctly
}
